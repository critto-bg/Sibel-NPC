/* react to player's hostility in the village */

IF
  InParty(Myself)
  Global("IaSVillageHostile","GLOBAL",1)
  OR(9)
    AreaCheck("ARIA28")
    AreaCheck("ARIA29")
    AreaCheck("ARIA30")
    AreaCheck("ARIA31")
    AreaCheck("ARIA32")
    AreaCheck("ARIA33")
    AreaCheck("ARIA34")
    AreaCheck("ARIA35")
    AreaCheck("ARIA36")
THEN
  RESPONSE #100
    SetGlobal("IaSVillageHostile","GLOBAL",2)
    StartDialogueNoSet(Player1)
END

/* personal quest */

IF
  InParty(Myself)
  Global("IaSibelDream","GLOBAL",0)
  XPGT(Myself,835000)
THEN
  RESPONSE #100
    SetGlobal("IaSibelDream","GLOBAL",1)
END

IF
  Global("IaSibelDream","GLOBAL",1)
  PartyRested()
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
  !AreaType(DUNGEON)
  !AreaCheck("AR3000")
THEN
  RESPONSE #100
    SetGlobal("IaSibelDream","GLOBAL",2)
    StartDialogueNoSet(Player1)
END

IF
  Global("IaSibelDream","GLOBAL",3)
  Global("EnteredArea","AR3000",1)
  AreaCheck("AR3000")
  InParty(Myself)
  See(Player1)
  Range(Player1,10)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelDream","GLOBAL",4)
    // the wait here if the party enters WK for the first time
    // the game will show the text screen with the intro in that case
    SetInterrupt(FALSE)
    Wait(3)
    StartDialogueNoSet(Player1)
    SetInterrupt(TRUE)
END

// piece of the root tree research banter
IF
  Global("IaLabFixed","ARIA31",4)
  Global("IaTreePlot","GLOBAL",1)
  AreaCheck("ARIA31")
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaTreePlot","GLOBAL",2)
    SetInterrupt(FALSE)
    Wait(1)
    StartDialogueNoSet(Player1)
    SetInterrupt(TRUE)
END

/* banters */

// first banter
IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",0)
  XPGT(Myself,560000)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",1)
    RealSetGlobalTimer("IaSibelDialogue","GLOBAL",300) // ONE_HOUR in game time, 5 min in real time
END

IF
  RealGlobalTimerExpired("IaSibelDialogue","GLOBAL")
  Global("IaSibelTalks","GLOBAL",1)
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
  !AreaType(DUNGEON)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",2)
    Interact(Player1)
END

// second banter
// happens if the first one has occurred
IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",3)
  Global("IaTreePlot","GLOBAL",6)
  Global("IaAmbervillePlot","GLOBAL",7)
  Dead("S!samber")
  XPGT(Myself,1385000)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",4)
    RealSetGlobalTimer("IaSibelDialogue","GLOBAL",300) // ONE_HOUR in game time, 5 min in real time
END

IF
  RealGlobalTimerExpired("IaSibelDialogue","GLOBAL")
  Global("IaSibelTalks","GLOBAL",4)
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",5)
    Interact(Player1)
END

// third banter
// happens separately from the first two
IF
  InParty(Myself)
  Global("IaSibelClericTalk","GLOBAL",0)
  Global("IaTreePlot","GLOBAL",6)
  Global("IaAmbervillePlot","GLOBAL",7)
  Dead("S!samber")
  XPGT(Myself,1660000)
  Class(Myself,CLERIC_ALL)
THEN
  RESPONSE #100
    SetGlobal("IaSibelClericTalk","GLOBAL",1)
    RealSetGlobalTimer("IaSibelClericTimer","GLOBAL",300) // ONE_HOUR in game time, 5 min in real time
END

IF
  RealGlobalTimerExpired("IaSibelClericTimer","GLOBAL")
  Global("IaSibelClericTalk","GLOBAL",1)
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelClericTalk","GLOBAL",2)
    Interact(Player1)
END

// fourth banter/event
// triggers only if the second banter has occurred
IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",6)
  Global("IaTreePlot","GLOBAL",6)
  Global("IaAmbervillePlot","GLOBAL",7)
  Dead("S!samber")
  XPGT(Myself,1810000)
  CombatCounter(0)
  !AreaType(DUNGEON)
  !Global("Chapter","GLOBAL",5)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",7)
    SetGlobalTimer("IaSibelMessTimer","GLOBAL",6)
END

IF
  RealGlobalTimerExpired("IaSibelDialogue","GLOBAL")
  Global("IaSibelTalks","GLOBAL",8)
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
THEN
  RESPONSE #100
    Interact(Player1)
END

// fifth banter
// happens only if the previous event has occurred

IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",9)
  XPGT(Myself,2210000)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",10)
    RealSetGlobalTimer("IaSibelDialogue","GLOBAL",300) // ONE_HOUR in game time, 5 min in real time
END

IF
  RealGlobalTimerExpired("IaSibelDialogue","GLOBAL")
  Global("IaSibelTalks","GLOBAL",10)
  InParty(Myself)
  See(Player1)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",11)
    Interact(Player1)
END

// sixth banter/event
// happens only if the previous event has occurred, non-evil path is chosen

IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",0)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  OR(2)
    AreaCheck("AR1600")
    AreaCheck("AR2100")
  GlobalGT("AsylumPlot","GLOBAL",49)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("IaSibelAmberAmbush","GLOBAL",1)
    Wait(3)
    Interact(Player1)
    SetInterrupt(TRUE)
END

IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",2)
  GlobalTimerExpired("IaSibelAmberTimer","GLOBAL")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("IaSibelAmberAmbush","GLOBAL",3)
    Wait(1)
    CreateCreatureObject("S!samb01",Player1,0,0,0)
    CreateCreatureObject("S!samb02","S!sibel",0,0,0)
    SetInterrupt(TRUE)
END

IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",3)
  CombatCounter(0)
  Dead("S!samb01")
  Dead("S!samb02")
THEN
  RESPONSE #100
    SetGlobal("IaSibelAmberAmbush","GLOBAL",4)
    Interact(Player1)
END

// seventh banter/event

IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",5)
  Global("IASJoinedToB","GLOBAL",0)
  Global("IaSGiftPlace","GLOBAL",6)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  OR(3)
    PartyHasItem("S!sglo01")
    PartyHasItem("S!sglo02")
    PartyHasItem("S!sglo03")
  OR(5)
    AreaCheck("AR0711")
    AreaCheck("AR3017")
    AreaCheck("AR0809")
    AreaCheck("AR2808")
    AreaCheck("AR5500")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("IaSibelAmberAmbush","GLOBAL",6)
    Wait(3)
    Interact(Player1)
    SetInterrupt(TRUE)
END

IF
  InParty(Myself)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",7)
  GlobalTimerExpired("IaSibelAmberTimer","GLOBAL")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("IaSibelAmberAmbush","GLOBAL",8)
    Wait(1)
    CreateCreatureObject("S!samb03",Player1,0,0,0) // Amber Golem
    CreateCreatureObject("S!samb04",Player1,0,0,0) // Mage
    CreateCreatureObject("S!samb05","S!sibel",0,0,0) // Berserker
    CreateCreatureObject("S!samb06",Player1,0,0,0) // Elf
    CreateCreatureObject("S!samb07","S!sibel",0,0,0) // Rune Assassin
    SetInterrupt(TRUE)
END

IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  Global("IaSibelAmberAmbush","GLOBAL",8)
  OR(5)
    AreaCheck("AR0711")
    AreaCheck("AR3017")
    AreaCheck("AR0809")
    AreaCheck("AR2808")
    AreaCheck("AR5500")
  CombatCounter(0)
  Dead("S!samb03")
  Dead("S!samb04")
  Dead("S!samb05")
  Dead("S!samb06")
  Dead("S!samb07")
THEN
  RESPONSE #100
    SetGlobal("IaSibelAmberAmbush","GLOBAL",9)
    Interact(Player1)
END

/* eighth banter/event */

// gift requested and found, one day has passed, party in SoA
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  GlobalGT("Chapter","GLOBAL",5)
  Global("IASJoinedToB","GLOBAL",0)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  Global("IaSGiftPlace","GLOBAL",6)
  GlobalTimerExpired("IaSGiftTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",13)
    SetGlobalTimer("IaSLeatherTimer","LOCALS",6)
END

// gift not requested, party enters Suldanesselar
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  Global("Chapter","GLOBAL",7)
  Global("IASJoinedToB","GLOBAL",0)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  Global("IaSGiftPlace","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",13)
    SetGlobalTimer("IaSLeatherTimer","LOCALS",6)
END

// gift not requested, party in ToB, one day passed since banter #5
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",12)
  GlobalGT("Chapter","GLOBAL",5)
  Global("IASJoinedToB","GLOBAL",1)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  Global("IaSGiftPlace","GLOBAL",0)
  GlobalTimerExpired("IaSResolutionTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",13)
    SetGlobalTimer("IaSLeatherTimer","LOCALS",6)
END

// golem messenger arrives
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",13)
  GlobalTimerExpired("IaSLeatherTimer","LOCALS")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",14)
    SetInterrupt(FALSE)
    CreateCreatureObjectDoor("S!sleat1",Myself,0,0,0)
    Wait(5)
    ActionOverride("S!sleat1",StartDialogueNoSet(Player1))
    SetInterrupt(TRUE)
END

// talk after the golem messenger has left
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",15)
  GlobalTimerExpired("IaSLeatherTimer","LOCALS")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",16)
    Interact(Player1)
END

// first timer to meet Virtus has expired
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",17)
  GlobalTimerExpired("IaSLeatherTimer","LOCALS")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",18)
    Interact(Player1)
END

// second timer to meet Virtus has expired
IF
  InParty(Myself)
  See(Player1)
  Global("IaSibelTalks","GLOBAL",19)
  GlobalTimerExpired("IaSLeatherTimer","LOCALS")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSibelTalks","GLOBAL",20)
    Interact(Player1)
END

/* nineth event */

// the first meeting with virtus
// the code to start it could be found in Copper Coronet's script (and other taverns in case of ToB)

/* tenth event */

IF
  InParty(Myself)
  See(Player1)
  Global("IaSRegiPlot","GLOBAL",0)
  Global("IaSamaraPlot","GLOBAL",11)
  GlobalGT("Chapter","GLOBAL",5)
  GlobalLT("Chapter","GLOBAL",8)
  Global("IASJoinedToB","GLOBAL",0)
  OR(2)
    Global("IaSibelFamilyPath","GLOBAL",1)
    Global("IaSibelFamilyPath","GLOBAL",2)
  XPGT(Myself,3034999)
THEN
  RESPONSE #100
    SetGlobal("IaSRegiPlot","GLOBAL",1)
    SetGlobalTimer("IaSGhostTimer","LOCALS",6)
END

IF
  InParty(Myself)
  See(Player1)
  Global("IaSRegiPlot","GLOBAL",1)
  GlobalTimerExpired("IaSGhostTimer","LOCALS")
  !AreaCheck("AR2500")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSRegiPlot","GLOBAL",2)
    SetInterrupt(FALSE)
    CreateCreatureObject("S!sghos1",Myself,0,0,0)
    Wait(1)
    ActionOverride("S!sghos1",StartDialogueNoSet(Player1))
    SetInterrupt(TRUE)
END

IF
  InParty(Myself)
  See(Player1)
  Global("IaSRegiPlot","GLOBAL",3)
  GlobalTimerExpired("IaSGhostTimer","LOCALS")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("IaSRegiPlot","GLOBAL",4)
    Interact(Player1)
END
