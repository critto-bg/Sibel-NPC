BACKUP ~SibelNPC/backup~
AUTHOR ~el.critto@gmail.com~

LANGUAGE "English"
  ~SibelNPC/lang/English/~
  ~SibelNPC/lang/English/setup.tra~

BEGIN @0
DESIGNATED 0
REQUIRE_FILE ~override/AMDK.2da~ @1

/* tables */

APPEND ~interdia.2da~
  ~S!SIBEL S!SibelB S!SibB25~
  UNLESS ~S!SIBEL~

APPEND ~mastarea.2da~ ~ARia28     value~
  UNLESS ~ARia28~

APPEND ~pdialog.2da~
  ~S!SIBEL S!SibelP S!SibelJ S!SibelD S!Sib25P S!Sib25J S!Sib25D S!Sibe25~
  UNLESS ~S!SIBEL~

COPY ~SibelNPC/resources/2da/S!sitems.2da~ ~override/S!sitems.2da~

/* items */

COPY ~SibelNPC/resources/items/S!slea01.itm~ ~override/S!slea01.itm~
  SAY NAME2 @500
  SAY DESC @501

  WRITE_ASCII 0x3a ~S!sleain~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!sblu01.itm~ ~override/S!sblu01.itm~
  SAY NAME2 @502
  SAY DESC @503

  WRITE_ASCII 0x3a ~S!scluin~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!sblu02.itm~ ~override/S!sblu02.itm~
  SAY NAME2 @504
  SAY DESC @505

  WRITE_ASCII 0x3a ~S!scluin~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!slea02.itm~ ~override/S!slea02.itm~
  SAY NAME2 @506
  SAY DESC @507

  WRITE_ASCII 0x3a ~S!sleain~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!slea03.itm~ ~override/S!slea03.itm~
  SAY NAME2 @506
  SAY DESC @508

  WRITE_ASCII 0x3a ~S!sleain~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!sglo01.itm~ ~override/S!sglo01.itm~
  SAY NAME2 @509
  SAY DESC @510

COPY ~SibelNPC/resources/items/S!sglo02.itm~ ~override/S!sglo02.itm~
  SAY NAME2 @509
  SAY DESC @511

COPY ~SibelNPC/resources/items/S!sglo03.itm~ ~override/S!sglo03.itm~
  SAY NAME2 @509
  SAY DESC @512

COPY ~SibelNPC/resources/items/S!sboo01.itm~ ~override/S!sboo01.itm~
  SAY NAME2 @513
  SAY DESC @514

  WRITE_ASCII 0x3a ~S!sboin~ #8 // inventory icon

COPY ~SibelNPC/resources/items/S!sgear.itm~ ~override/S!sgear.itm~
  SAY NAME2 @515
  SAY DESC @516

COPY ~SibelNPC/resources/items/S!swren.itm~ ~override/S!swren.itm~
  SAY NAME2 @517
  SAY DESC @518

COPY ~SibelNPC/resources/items/S!ssaw.itm~ ~override/S!ssaw.itm~
  SAY NAME2 @519
  SAY DESC @520

COPY ~SibelNPC/resources/items/S!scoin.itm~ ~override/S!scoin.itm~
  SAY NAME2 @521
  SAY DESC @522

COPY ~SibelNPC/resources/items/S!spowda.itm~ ~override/S!spowda.itm~
  SAY NAME2 @523
  SAY DESC @524

COPY ~SibelNPC/resources/items/S!spowdb.itm~ ~override/S!spowdb.itm~
  SAY NAME2 @525
  SAY DESC @526

COPY ~SibelNPC/resources/items/S!spowdc.itm~ ~override/S!spowdc.itm~
  SAY NAME2 @527
  SAY DESC @528

COPY ~SibelNPC/resources/items/S!santi.itm~ ~override/S!santi.itm~
  SAY NAME2 @529
  SAY DESC @530

COPY ~SibelNPC/resources/items/S!slthr1.itm~ ~override/S!slthr1.itm~
  SAY NAME2 @531
  SAY DESC @532

COPY ~SibelNPC/resources/items/S!slthr2.itm~ ~override/S!slthr2.itm~
  SAY NAME2 @533
  SAY DESC @534

COPY ~SibelNPC/resources/items/S!sroot.itm~ ~override/S!sroot.itm~
  SAY NAME2 @535
  SAY DESC @536

COPY ~SibelNPC/resources/items/S!sjour.itm~ ~override/S!sjour.itm~
  SAY NAME1 @537
  SAY NAME2 @537
  SAY UNIDENTIFIED_DESC @538
  SAY DESC @538

COPY ~SibelNPC/resources/items/S!sakey.itm~ ~override/S!sakey.itm~
  SAY NAME1 @539
  SAY NAME2 @539
  SAY UNIDENTIFIED_DESC @540
  SAY DESC @540

COPY ~SibelNPC/resources/items/S!sfood.itm~ ~override/S!sfood.itm~
  SAY NAME1 @541
  SAY NAME2 @541
  SAY UNIDENTIFIED_DESC @542
  SAY DESC @542

COPY ~SibelNPC/resources/items/S!sleaj.itm~ ~override/S!sleaj.itm~
  SAY NAME1 @543
  SAY NAME2 @543
  SAY UNIDENTIFIED_DESC @544

COPY ~SibelNPC/resources/items/S!schunk.itm~ ~override/S!schunk.itm~
  SAY NAME2 @546
  SAY DESC @547

COPY ~SibelNPC/resources/items/S!ssama.itm~ ~override/S!ssama.itm~
  SAY NAME2 @548
  SAY DESC @549

COPY ~SibelNPC/resources/items/S!samul.itm~ ~override/S!samul.itm~
  SAY NAME2 @550
  SAY DESC @551

COPY ~SibelNPC/resources/items/S!slet01.itm~ ~override/S!slet01.itm~
  SAY NAME1 @552
  SAY NAME2 @552
  SAY DESC @553

// classic ToB only
ACTION_IF (!FILE_EXISTS ~engine.lua~) THEN BEGIN
  COPY ~SibelNPC/resources/items/S!smisc.itm~ ~override/S!smisc.itm~
    SAY NAME1 @554
    SAY NAME2 @554

  COPY ~SibelNPC/resources/items/S!sw04.itm~ ~override/S!sw04.itm~
    SAY NAME1 @555
    SAY NAME2 @555
    SAY DESC @556
END

COPY ~SibelNPC/resources/items/S!sramb.itm~ ~override/S!sramb.itm~
  SAY NAME1 @557
  SAY NAME2 @557
  SAY DESC @558

COPY ~SibelNPC/resources/items/S!sgamb.itm~ ~override/S!sgamb.itm~

COPY ~SibelNPC/resources/items/S!sioun1.itm~ ~override/S!sioun1.itm~
  SAY NAME1 @559
  SAY NAME2 @559
  SAY DESC @560

COPY ~SibelNPC/resources/items/S!sesse.itm~ ~override/S!sesse.itm~
  SAY NAME2 @561
  SAY DESC @562

COPY ~SibelNPC/resources/items/S!sgamul.itm~ ~override/S!sgamul.itm~
  SAY NAME2 @563
  SAY DESC @564

COPY ~SibelNPC/resources/items/S!schan1.itm~ ~override/S!schan1.itm~
  SAY NAME2 @565
  SAY DESC @566

COPY ~SibelNPC/resources/items/S!splat1.itm~ ~override/S!splat1.itm~
  SAY NAME2 @567
  SAY DESC @568

COPY ~SibelNPC/resources/items/S!splat2.itm~ ~override/S!splat2.itm~
  SAY NAME2 @571
  SAY DESC @572

COPY ~SibelNPC/resources/items/S!shorn.itm~ ~override/S!shorn.itm~
  SAY NAME2 @573
  SAY DESC @574

COPY ~SibelNPC/resources/items/S!shelm1.itm~ ~override/S!shelm1.itm~
  SAY NAME2 @575
  SAY DESC @576

ACTION_IF (!FILE_EXISTS ~engine.lua~) THEN BEGIN
  COPY_EXISTING ~potn37.itm~ ~override/potn37.itm~
    WRITE_SHORT 0x38 99 // max in stack amount

  COPY_EXISTING ~S!misc21.itm~ ~override/S!misc21.itm~
    SAY NAME2 @569
    SAY DESC @570
END

/* creatures */

COPY ~SibelNPC/resources/creatures/S!sibel.cre~ ~override/S!sibel.cre~
  SAY NAME1 @10
  SAY NAME2 @10
  SAY BIO @11

  PATCH_IF (FILE_EXISTS ~engine.lua~) BEGIN
    WRITE_ASCII 0x34 ~S!SIBELM~ #8 // small portrait
    WRITE_ASCII 0x3c ~S!SIBELL~ #8 // large portrait

    READ_BYTE 0x12 "flags"
    WRITE_BYTE 0x12 ("%flags%" BOR 0b01000000) // set EE: ignore nightmare mode flag
  END
  ELSE BEGIN
    WRITE_ASCII 0x34 ~S!SIBELS~ #8 // small portrait
    WRITE_ASCII 0x3c ~S!SIBELM~ #8 // large portrait
  END

  WRITE_ASCII 0x0248 ~S!SIBEL~ #8 // override script
  WRITE_ASCII 0x0280 ~S!SIBEL~ #32 // death variable
  WRITE_ASCII 0x02cc ~S!SIBEL~ #8 // dialog

  SAY LEADER @1000
  SAY TIRED @1001
  SAY BORED @1002
  SAY BATTLE_CRY1 @1003
  SAY BATTLE_CRY2 @1004
  SAY BATTLE_CRY3 @1005
  SAY DAMAGE @1006
  SAY DYING @1007
  SAY HURT @1008
  SAY SELECT_COMMON1 @1009
  SAY SELECT_COMMON2 @1010
  SAY SELECT_COMMON3 @1011
  SAY SELECT_COMMON4 @1012
  SAY SELECT_ACTION1 @1013
  SAY SELECT_ACTION2 @1014
  SAY SELECT_ACTION3 @1015
  SAY SELECT_ACTION4 @1016
  SAY SELECT_ACTION5 @1017
  SAY SELECT_ACTION6 @1018
  SAY SELECT_ACTION7 @1019
  SAY REACT_TO_DIE_GENERAL @1020

  SAY SELECT_RARE1 #-1
  SAY SELECT_RARE2 #-1
  SAY CRITICAL_HIT #-1
  SAY CRITICAL_MISS #-1
  SAY TARGET_IMMUNE #-1
  SAY INVENTORY_FULL #-1
  SAY PICKED_POCKET #-1
  SAY HIDDEN_IN_SHADOWS #-1
  SAY SPELL_DISRUPTED #-1
  SAY SET_A_TRAP #-1

  ADD_CRE_ITEM ~S!slea01~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~S!sblu01~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~

  // set equipped weapon slot
  READ_LONG 0x02b8 "item_slots_offset"
  WRITE_SHORT ("%item_slots_offset%" + 0x4C) 0

COPY ~SibelNPC/resources/creatures/S!sinnk.cre~ ~override/S!sinnk.cre~
  SAY NAME1 #12330
  SAY NAME2 #12330
  WRITE_ASCII 0x02cc ~S!SINNK~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!scomm1.cre~ ~override/S!scomm1.cre~
     ~SibelNPC/resources/creatures/S!scomm2.cre~ ~override/S!scomm2.cre~
     ~SibelNPC/resources/creatures/S!scomm3.cre~ ~override/S!scomm3.cre~
     ~SibelNPC/resources/creatures/S!scomm4.cre~ ~override/S!scomm4.cre~
     ~SibelNPC/resources/creatures/S!scomm5.cre~ ~override/S!scomm5.cre~
     ~SibelNPC/resources/creatures/S!scomm6.cre~ ~override/S!scomm6.cre~

  WRITE_ASCII 0x0248 ~SHOUT~ #8 // override script
  WRITE_ASCII 0x0250 ~~ #8 // class script
  WRITE_ASCII 0x0258 ~RUNENEMY~ #8 // race script
  WRITE_ASCII 0x0260 ~~ #8 // general script
  WRITE_ASCII 0x0268 ~WDRUNSGT~ #8 // default script
  WRITE_ASCII 0x0280 ~None~ #32 // death variable
  WRITE_ASCII 0x02cc ~S!SCOMM~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!strist.cre~ ~override/S!strist.cre~
  SAY NAME1 @21
  SAY NAME2 @21

  WRITE_ASCII 0x0248 ~~ #8 // override script
  WRITE_ASCII 0x02cc ~S!STRIST~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!slena.cre~ ~override/S!slena.cre~
  SAY NAME1 @22
  SAY NAME2 @22

  WRITE_ASCII 0x0248 ~S!SLENA~ #8 // override script
  WRITE_ASCII 0x0250 ~RUNENEMY~ #8 // class script
  WRITE_ASCII 0x02cc ~S!SLENA~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!sflava.cre~ ~override/S!sflava.cre~
  SAY NAME1 @23
  SAY NAME2 @23

  WRITE_ASCII 0x0248 ~S!SFLAVA~ #8 // override script
  WRITE_ASCII 0x02cc ~S!SFLAVA~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!sagent.cre~ ~override/S!sagent.cre~
  SAY NAME1 @24
  SAY NAME2 @24

  WRITE_ASCII 0x0248 ~S!SAGENT~ #8 // override script
  WRITE_ASCII 0x02cc ~S!SAGENT~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!scat.cre~ ~override/S!scat.cre~
  SAY NAME1 @25
  SAY NAME2 @25

  WRITE_ASCII 0x0248 ~S!SCAT~ #8 // override script
  WRITE_ASCII 0x0250 ~RUNENEMY~ #8 // class script
  WRITE_ASCII 0x0268 ~~ #8 // default script
  WRITE_ASCII 0x02cc ~S!SCAT~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!szoe.cre~ ~override/S!szoe.cre~
  SAY NAME1 @26
  SAY NAME2 @26

  WRITE_ASCII 0x0248 ~S!SZOE~ #8 // override script
  WRITE_ASCII 0x02cc ~S!SZOE~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!smarce.cre~ ~override/S!smarce.cre~
  SAY NAME1 @27
  SAY NAME2 @27

  WRITE_ASCII 0x02cc ~S!SMARCE~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!showa.cre~ ~override/S!showa.cre~
  SAY NAME1 @28
  SAY NAME2 @28

  WRITE_ASCII 0x0248 ~S!SHOWA~ #8 // override script
  WRITE_ASCII 0x02cc ~S!SHOWA~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!swill.cre~ ~override/S!swill.cre~
  SAY NAME1 @29
  SAY NAME2 @29

  WRITE_ASCII 0x0250 ~RUNENEMY~ #8 // class script
  WRITE_ASCII 0x02cc ~S!SWILL~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!samber.cre~ ~override/S!samber.cre~
     ~SibelNPC/resources/creatures/S!samb01.cre~ ~override/S!samb01.cre~
     ~SibelNPC/resources/creatures/S!samb02.cre~ ~override/S!samb02.cre~
     ~SibelNPC/resources/creatures/S!samb03.cre~ ~override/S!samb03.cre~
     ~SibelNPC/resources/creatures/S!samb04.cre~ ~override/S!samb04.cre~
     ~SibelNPC/resources/creatures/S!samb05.cre~ ~override/S!samb05.cre~
     ~SibelNPC/resources/creatures/S!samb06.cre~ ~override/S!samb06.cre~
  SAY NAME1 @30
  SAY NAME2 @30

COPY ~SibelNPC/resources/creatures/S!ssmil.cre~ ~override/S!ssmil.cre~
  SAY NAME1 @31
  SAY NAME2 @31

  WRITE_ASCII 0x0248 ~S!SSMIL~ #8 // override script

COPY ~SibelNPC/resources/creatures/S!sregi.cre~ ~override/S!sregi.cre~
  SAY NAME1 @32
  SAY NAME2 @32

  WRITE_ASCII 0x0248 ~S!sregi~ #8 // override script
  WRITE_ASCII 0x02cc ~S!sregi~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!ssqui.cre~ ~override/S!ssqui.cre~
  SAY NAME1 @33
  SAY NAME2 @33

  WRITE_ASCII 0x0248 ~S!ssqui~ #8 // override script
  WRITE_ASCII 0x02cc ~S!ssqui~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!ssnake.cre~ ~override/S!ssnake.cre~
  SAY NAME1 @34
  SAY NAME2 @34

  WRITE_ASCII 0x0248 ~S!ssnake~ #8 // override script

COPY ~SibelNPC/resources/creatures/S!sboy01.cre~ ~override/S!sboy01.cre~
  SAY NAME1 @35
  SAY NAME2 @35

  WRITE_ASCII 0x02cc ~S!sboy01~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!sgamb.cre~ ~override/S!sgamb.cre~
  SAY NAME1 @36
  SAY NAME2 @36

COPY ~SibelNPC/resources/creatures/S!selow.cre~ ~override/S!selow.cre~
  SAY NAME1 @37
  SAY NAME2 @37

  WRITE_ASCII 0x0248 ~S!selow~ #8 // override script
  WRITE_ASCII 0x02cc ~S!selow~ #8 // dialog

COPY ~SibelNPC/resources/creatures/S!sbers.cre~ ~override/S!sbers.cre~
  SAY NAME1 @38
  SAY NAME2 @38

// classic ToB only
ACTION_IF (!FILE_EXISTS ~engine.lua~) THEN BEGIN
  // Dracolich
  COPY_EXISTING ~S!dracol.cre~ ~override/S!dracol.cre~
    WRITE_LONG 0x001c 25000 // gold
    ADD_CRE_ITEM ~shld22~ #0 #0 #0 ~UNSTEALABLE~ ~INV1~ // Sentinel +4

  // Vadek
  COPY_EXISTING ~pirmur07.cre~ ~override/pirmur07.cre~
    ADD_CRE_ITEM ~S!smisc~ #0 #0 #0 ~UNSTEALABLE~ ~INV1~ // Necklace of the Rich
END

/* spells */

COPY ~SibelNPC/resources/spells/S!swiz.spl~ ~override/S!swiz.spl~
  SAY NAME1 @701
  SAY NAME2 @701

COPY_EXISTING ~SPPR713.spl~ ~override/SPPR713.spl~
  SAY UNIDENTIFIED_DESC @700

/* effects */

COPY ~SibelNPC/resources/effects/S!shorn.eff~ ~override/S!shorn.eff~

/* stores */

// classic ToB only
ACTION_IF (!FILE_EXISTS ~engine.lua~) THEN BEGIN
  COPY_EXISTING ~uddrow23.sto~ ~override/uddrow23.sto~
    READ_LONG 0x38 "itemscount"

    PATCH_IF ("%itemscount%" > 0) BEGIN
      READ_LONG 0x2C "purchasedoffset"
      READ_LONG 0x34 "itemsoffset"
      READ_LONG 0x4C "drinksoffset"
      READ_LONG 0x70 "curesoffset"

      SET "removeditems" = 0
      SET "cnt" = "%itemscount%" - 1

      WHILE ("%cnt%" >= 0) BEGIN
        READ_ASCII ("%itemsoffset%" + "%cnt%"*28) "itemname"
        PATCH_IF ("%itemname%" STRING_COMPARE_CASE "shld22" = 0)
        BEGIN
          DELETE_BYTES ("%itemsoffset%" + ("%cnt%"*28)) 28
          SET "removeditems" = "%removeditems%" + 1
        END

        SET "cnt" = "%cnt%" - 1
      END

      WRITE_LONG 0x38 ("%itemscount%" - "%removeditems%")

      PATCH_IF ("%purchasedoffset%" > "%itemsoffset%") BEGIN WRITE_LONG 0x2C ("%purchasedoffset%" - ("%removeditems%" * 28)) END
      PATCH_IF ("%drinksoffset%" > "%itemsoffset%") BEGIN WRITE_LONG 0x4C ("%drinksoffset%" - ("%removeditems%" * 28)) END
      PATCH_IF ("%curesoffset%" > "%itemsoffset%") BEGIN WRITE_LONG 0x70 ("%curesoffset%" - ("%removeditems%" * 28)) END
    END
END

/* dialogues */

COMPILE ~SibelNPC/resources/dialogues/agent.d~
COMPILE ~SibelNPC/resources/dialogues/cat.d~
COMPILE ~SibelNPC/resources/dialogues/commoner.d~
COMPILE ~SibelNPC/resources/dialogues/flava.d~
COMPILE ~SibelNPC/resources/dialogues/innkeep.d~
COMPILE ~SibelNPC/resources/dialogues/lena.d~
COMPILE ~SibelNPC/resources/dialogues/marcel.d~
COMPILE ~SibelNPC/resources/dialogues/sibel.d~
COMPILE ~SibelNPC/resources/dialogues/sibelb.d~
COMPILE ~SibelNPC/resources/dialogues/sibelj.d~
COMPILE ~SibelNPC/resources/dialogues/sibelp.d~
COMPILE ~SibelNPC/resources/dialogues/howard.d~
COMPILE ~SibelNPC/resources/dialogues/tristan.d~
COMPILE ~SibelNPC/resources/dialogues/willard.d~
COMPILE ~SibelNPC/resources/dialogues/zoe.d~
COMPILE ~SibelNPC/resources/dialogues/celvan.d~
COMPILE ~SibelNPC/resources/dialogues/reginald.d~
COMPILE ~SibelNPC/resources/dialogues/squirrel.d~
COMPILE ~SibelNPC/resources/dialogues/boy.d~
COMPILE ~SibelNPC/resources/dialogues/elowen.d~

/* scripts */

COMPILE ~SibelNPC/resources/scripts/aria28.baf~
COMPILE ~SibelNPC/resources/scripts/aria32.baf~
COMPILE ~SibelNPC/resources/scripts/aria33.baf~
COMPILE ~SibelNPC/resources/scripts/aria34.baf~
COMPILE ~SibelNPC/resources/scripts/aria35.baf~
COMPILE ~SibelNPC/resources/scripts/S!sagent.baf~
COMPILE ~SibelNPC/resources/scripts/S!scat.baf~
COMPILE ~SibelNPC/resources/scripts/S!scauld.baf~
COMPILE ~SibelNPC/resources/scripts/S!scut01.baf~
COMPILE ~SibelNPC/resources/scripts/S!scut02.baf~
COMPILE ~SibelNPC/resources/scripts/S!scut03.baf~
COMPILE ~SibelNPC/resources/scripts/S!scut04.baf~
COMPILE ~SibelNPC/resources/scripts/S!scutf1.baf~
COMPILE ~SibelNPC/resources/scripts/S!scutf2.baf~
COMPILE ~SibelNPC/resources/scripts/S!scutf3.baf~
COMPILE ~SibelNPC/resources/scripts/S!scutf4.baf~
COMPILE ~SibelNPC/resources/scripts/S!scutf5.baf~
COMPILE ~SibelNPC/resources/scripts/S!selow.baf~
COMPILE ~SibelNPC/resources/scripts/S!sflava.baf~
COMPILE ~SibelNPC/resources/scripts/S!showa.baf~
COMPILE ~SibelNPC/resources/scripts/S!sibel.baf~
COMPILE ~SibelNPC/resources/scripts/S!slena.baf~
COMPILE ~SibelNPC/resources/scripts/S!ssmil.baf~
COMPILE ~SibelNPC/resources/scripts/S!ssnake.baf~
COMPILE ~SibelNPC/resources/scripts/S!ssqui.baf~
COMPILE ~SibelNPC/resources/scripts/S!stree.baf~
COMPILE ~SibelNPC/resources/scripts/S!szoe.baf~

EXTEND_BOTTOM ~ARIA11.BCS~ ~SibelNPC/resources/scripts/aria11.baf~
EXTEND_BOTTOM ~AR0516.BCS~ ~SibelNPC/resources/scripts/AR0516.baf~
EXTEND_BOTTOM ~AR0702.BCS~ ~SibelNPC/resources/scripts/AR0702.baf~
EXTEND_BOTTOM ~AR0809.BCS~ ~SibelNPC/resources/scripts/AR0809.baf~
EXTEND_BOTTOM ~AR2209.BCS~ ~SibelNPC/resources/scripts/AR2209.baf~
EXTEND_BOTTOM ~AR2808.BCS~ ~SibelNPC/resources/scripts/AR2808.baf~
EXTEND_BOTTOM ~AR3000.BCS~ ~SibelNPC/resources/scripts/AR3000.baf~
EXTEND_BOTTOM ~AR3017.BCS~ ~SibelNPC/resources/scripts/AR3017.baf~
EXTEND_BOTTOM ~AR5503.BCS~ ~SibelNPC/resources/scripts/AR5503.baf~
EXTEND_BOTTOM ~BALDUR.BCS~ ~SibelNPC/resources/scripts/BALDUR.baf~
EXTEND_BOTTOM ~BALDUR25.BCS~ ~SibelNPC/resources/scripts/BALDUR25.baf~

EXTEND_TOP ~AR0334.BCS~ ~SibelNPC/resources/scripts/AR0334.baf~
EXTEND_TOP ~S!BOUNTY.BCS~ ~SibelNPC/resources/scripts/S!bounty.baf~
EXTEND_TOP ~S!WARDEN.BCS~ ~SibelNPC/resources/scripts/S!warden.baf~

// classic ToB only
ACTION_IF (!FILE_EXISTS ~engine.lua~) THEN BEGIN
  EXTEND_TOP ~AR0334.BCS~ ~SibelNPC/resources/scripts/AR0334_JD.baf~
  EXTEND_TOP ~MAGE20A.BCS~ ~SibelNPC/resources/scripts/mage20a.baf~

  EXTEND_BOTTOM ~AR0204.BCS~ ~SibelNPC/resources/scripts/AR0204.baf~
  EXTEND_BOTTOM ~AR0700.BCS~ ~SibelNPC/resources/scripts/AR0700_JD.baf~
  EXTEND_BOTTOM ~AR3000.BCS~ ~SibelNPC/resources/scripts/AR3000_JD.baf~
  EXTEND_BOTTOM ~AR4500.BCS~ ~SibelNPC/resources/scripts/AR4500_JD.baf~
  EXTEND_BOTTOM ~S!fsdrag.BCS~ ~SibelNPC/resources/scripts/S!fsdrag.baf~
  EXTEND_BOTTOM ~S!torgal.BCS~ ~SibelNPC/resources/scripts/S!torgal.baf~
END

/* graphics */

COPY ~SibelNPC/resources/portraits/SibelS.BMP~ ~override/S!SIBELS.BMP~
COPY ~SibelNPC/resources/portraits/SibeLM.BMP~ ~override/S!SIBELM.BMP~
COPY ~SibelNPC/resources/portraits/SibelL.BMP~ ~override/S!SIBELL.BMP~

ACTION_IF (FILE_EXISTS ~engine.lua~) THEN BEGIN
  COPY ~SibelNPC/resources/portraits/ElowenL.BMP~ ~override/ElowenS.bmp~
END
ELSE BEGIN
  COPY ~SibelNPC/resources/portraits/ElowenS.BMP~ ~override/ElowenS.bmp~
END

COPY ~SibelNPC/resources/bam/2102FP1.BAM~ ~override/2102FP1.BAM~
COPY ~SibelNPC/resources/bam/2102FP2.BAM~ ~override/2102FP2.BAM~
COPY ~SibelNPC/resources/bam/S!powdb.bam~ ~override/S!powdb.bam~
COPY ~SibelNPC/resources/bam/S!powdc.bam~ ~override/S!powdc.bam~
COPY ~SibelNPC/resources/bam/S!samul.bam~ ~override/S!samul.bam~
COPY ~SibelNPC/resources/bam/S!santi.bam~ ~override/S!santi.bam~
COPY ~SibelNPC/resources/bam/S!sboin.bam~ ~override/S!sboin.bam~
COPY ~SibelNPC/resources/bam/S!scluin.bam~ ~override/S!scluin.bam~
COPY ~SibelNPC/resources/bam/S!scoin.bam~ ~override/S!scoin.bam~
COPY ~SibelNPC/resources/bam/S!sfooin.bam~ ~override/S!sfooin.bam~
COPY ~SibelNPC/resources/bam/S!sgear.bam~ ~override/S!sgear.bam~
COPY ~SibelNPC/resources/bam/S!shorn.bam~ ~override/S!shorn.bam~
COPY ~SibelNPC/resources/bam/S!siouin.bam~ ~override/S!siouin.bam~
COPY ~SibelNPC/resources/bam/S!sleain.bam~ ~override/S!sleain.bam~
COPY ~SibelNPC/resources/bam/S!slthr1.bam~ ~override/S!slthr1.bam~
COPY ~SibelNPC/resources/bam/S!slthr2.bam~ ~override/S!slthr2.bam~
COPY ~SibelNPC/resources/bam/S!splat1.bam~ ~override/S!splat1.bam~
COPY ~SibelNPC/resources/bam/S!sramb.bam~ ~override/S!sramb.bam~
COPY ~SibelNPC/resources/bam/S!sroot.bam~ ~override/S!sroot.bam~
COPY ~SibelNPC/resources/bam/S!ssain.bam~ ~override/S!ssain.bam~
COPY ~SibelNPC/resources/bam/S!ssawin.bam~ ~override/S!ssawin.bam~

/* areas */

// outside area
COPY ~SibelNPC/resources/areas/aria28.are~ ~override/aria28.are~
  WRITE_ASCII 0x94 ~aria28~ #8 // area script

  // update triggers //

  READ_SHORT 0x5a triggers_amount
  READ_LONG 0x5c triggers_offset

  FOR (index = 0; index < triggers_amount; index = index + 1) BEGIN
    READ_ASCII (triggers_offset + (0xc4 * index)) trigger_name (32)

    // General Store
    PATCH_IF ("%trigger_name%" STRING_MATCHES_REGEXP "General Store" = 0) BEGIN
      SAY (triggers_offset + 0x64 + (0xc4 * index)) @13
    END

    // Inn Sign
    PATCH_IF ("%trigger_name%" STRING_MATCHES_REGEXP "Inn Sign" = 0) BEGIN
      SAY (triggers_offset + 0x64 + (0xc4 * index)) @15
    END

    // Tavern Sign
    PATCH_IF ("%trigger_name%" STRING_MATCHES_REGEXP "Tavern Sign" = 0) BEGIN
      SAY (triggers_offset + 0x64 + (0xc4 * index)) @18
    END
  END

  // update labels for automap notes //

  READ_LONG 0xc4 map_notes_offset
  READ_LONG 0xc8 map_notes_amount

  FOR (index = 0; index < map_notes_amount; index = index + 1) BEGIN
    READ_SHORT (map_notes_offset        + (0x34 * index)) x_coord
    READ_SHORT (map_notes_offset + 0x02 + (0x34 * index)) y_coord

    // Lord Arcanis' Home
    PATCH_IF (x_coord = 1430 AND y_coord = 1970) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @12
    END

    // General Store
    PATCH_IF (x_coord = 1160 AND y_coord = 1800) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @13
    END

    // Alchemy Tower
    PATCH_IF (x_coord = 390 AND y_coord = 655) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @14
    END

    // Village Inn
    PATCH_IF (x_coord = 1210 AND y_coord = 380) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @15
    END

    // Howard's Workshop
    PATCH_IF (x_coord = 1520 AND y_coord = 250) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @16
    END

    // Amberville Home
    PATCH_IF (x_coord = 2770 AND y_coord = 1147) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @17
    END

    // Root Cellar
    PATCH_IF (x_coord = 2085 AND y_coord = 780) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @18
    END

    // Library
    PATCH_IF (x_coord = 2880 AND y_coord = 630) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @19
    END

    // Empty House
    PATCH_IF (x_coord = 2008 AND y_coord = 1550) BEGIN
      SAY (map_notes_offset + 0x04 + (0x34 * index)) @20
    END
  END

// Lord Arcanis' Home
COPY ~SibelNPC/resources/areas/aria29.are~ ~override/aria29.are~
  WRITE_ASCII 0x94 ~aria29~ #8 // area script

// General Store
COPY ~SibelNPC/resources/areas/aria30.are~ ~override/aria30.are~
  WRITE_ASCII 0x94 ~aria30~ #8 // area script

// Alchemy Tower
COPY ~SibelNPC/resources/areas/aria31.are~ ~override/aria31.are~
  WRITE_ASCII 0x94 ~aria31~ #8 // area script

  // update triggers //

  READ_SHORT 0x5a triggers_amount
  READ_LONG 0x5c triggers_offset

  FOR (index = 0; index < triggers_amount; index = index + 1) BEGIN
    READ_ASCII (triggers_offset + (0xc4 * index)) trigger_name (32)

    // Cauldron
    PATCH_IF ("%trigger_name%" STRING_MATCHES_REGEXP "Cauldron" = 0) BEGIN
      WRITE_ASCII (triggers_offset + 0x7c + (0xc4 * index)) ~S!scauld~ #8 // region script
    END
  END

// Village Inn
COPY ~SibelNPC/resources/areas/aria32.are~ ~override/aria32.are~
  WRITE_ASCII 0x94 ~aria32~ #8 // area script

// Howard's Workshop
COPY ~SibelNPC/resources/areas/aria33.are~ ~override/aria33.are~
  WRITE_ASCII 0x94 ~aria33~ #8 // area script

// Amberville Home
COPY ~SibelNPC/resources/areas/aria34.are~ ~override/aria34.are~
  WRITE_ASCII 0x94 ~aria34~ #8 // area script

// Root Cellar
COPY ~SibelNPC/resources/areas/aria35.are~ ~override/aria35.are~
  WRITE_ASCII 0x94 ~aria35~ #8 // area script

  // update triggers //

  READ_SHORT 0x5a triggers_amount
  READ_LONG 0x5c triggers_offset

  FOR (index = 0; index < triggers_amount; index = index + 1) BEGIN
    READ_ASCII (triggers_offset + (0xc4 * index)) trigger_name (32)

    // Tree Root
    PATCH_IF ("%trigger_name%" STRING_MATCHES_REGEXP "Tree Root" = 0) BEGIN
      WRITE_ASCII (triggers_offset + 0x7c + (0xc4 * index)) ~S!stree~ #8 // region script
    END
  END

// Library
COPY ~SibelNPC/resources/areas/aria36.are~ ~override/aria36.are~
  WRITE_ASCII 0x94 ~aria36~ #8 // area script

// Amkethran, Chyil's House
COPY_EXISTING ~AR5503.are~ ~override/AR5503.are~
  WRITE_ASCII 0x94 ~AR5503~ #8 // area script

/* area resources */

COPY ~SibelNPC/resources/areas/aria28.wed~ ~override/aria28.wed~
COPY ~SibelNPC/resources/areas/aria28HT.bmp~ ~override/aria28HT.bmp~
COPY ~SibelNPC/resources/areas/aria28LM.bmp~ ~override/aria28LM.bmp~
COPY ~SibelNPC/resources/areas/aria28SR.bmp~ ~override/aria28SR.bmp~

COPY ~SibelNPC/resources/areas/aria29.wed~ ~override/aria29.wed~
COPY ~SibelNPC/resources/areas/aria29HT.bmp~ ~override/aria29HT.bmp~
COPY ~SibelNPC/resources/areas/aria29LM.bmp~ ~override/aria29LM.bmp~
COPY ~SibelNPC/resources/areas/aria29SR.bmp~ ~override/aria29SR.bmp~

COPY ~SibelNPC/resources/areas/aria30.wed~ ~override/aria30.wed~
COPY ~SibelNPC/resources/areas/aria30HT.bmp~ ~override/aria30HT.bmp~
COPY ~SibelNPC/resources/areas/aria30LM.bmp~ ~override/aria30LM.bmp~
COPY ~SibelNPC/resources/areas/aria30SR.bmp~ ~override/aria30SR.bmp~

COPY ~SibelNPC/resources/areas/aria31.wed~ ~override/aria31.wed~
COPY ~SibelNPC/resources/areas/aria31HT.bmp~ ~override/aria31HT.bmp~
COPY ~SibelNPC/resources/areas/aria31LM.bmp~ ~override/aria31LM.bmp~
COPY ~SibelNPC/resources/areas/aria31SR.bmp~ ~override/aria31SR.bmp~

COPY ~SibelNPC/resources/areas/aria33.wed~ ~override/aria33.wed~
COPY ~SibelNPC/resources/areas/aria33HT.bmp~ ~override/aria33HT.bmp~
COPY ~SibelNPC/resources/areas/aria33LM.bmp~ ~override/aria33LM.bmp~
COPY ~SibelNPC/resources/areas/aria33SR.bmp~ ~override/aria33SR.bmp~

COPY ~SibelNPC/resources/areas/aria36.wed~ ~override/aria36.wed~
COPY ~SibelNPC/resources/areas/aria36HT.bmp~ ~override/aria36HT.bmp~
COPY ~SibelNPC/resources/areas/aria36LM.bmp~ ~override/aria36LM.bmp~
COPY ~SibelNPC/resources/areas/aria36SR.bmp~ ~override/aria36SR.bmp~

ACTION_IF (FILE_EXISTS ~engine.lua~) THEN BEGIN
  COPY ~SibelNPC/resources/areas/aria28_ee.mos~ ~override/aria28.mos~
  COPY ~SibelNPC/resources/areas/aria28_ee.tis~ ~override/aria28.tis~
  COPY ~SibelNPC/resources/areas/AIA2800.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2801.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2802.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2803.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2804.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2805.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2806.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2807.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2808.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2809.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2810.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2811.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2812.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2813.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2814.PVRZ~ ~override~
  COPY ~SibelNPC/resources/areas/AIA2815.PVRZ~ ~override~

  COPY ~SibelNPC/resources/areas/aria29_ee.mos~ ~override/aria29.mos~
  COPY ~SibelNPC/resources/areas/aria29_ee.tis~ ~override/aria29.tis~
  COPY ~SibelNPC/resources/areas/AIA2900.PVRZ~ ~override~

  COPY ~SibelNPC/resources/areas/aria30_ee.mos~ ~override/aria30.mos~
  COPY ~SibelNPC/resources/areas/aria30_ee.tis~ ~override/aria30.tis~
  COPY ~SibelNPC/resources/areas/AIA3000.PVRZ~ ~override~

  COPY ~SibelNPC/resources/areas/aria31_ee.mos~ ~override/aria31.mos~
  COPY ~SibelNPC/resources/areas/aria31_ee.tis~ ~override/aria31.tis~
  COPY ~SibelNPC/resources/areas/AIA3100.PVRZ~ ~override~

  COPY ~SibelNPC/resources/areas/aria33_ee.mos~ ~override/aria33.mos~
  COPY ~SibelNPC/resources/areas/aria33_ee.tis~ ~override/aria33.tis~
  COPY ~SibelNPC/resources/areas/AIA3300.PVRZ~ ~override~

  COPY ~SibelNPC/resources/areas/aria36_ee.mos~ ~override/aria36.mos~
  COPY ~SibelNPC/resources/areas/aria36_ee.tis~ ~override/aria36.tis~
  COPY ~SibelNPC/resources/areas/AIA3600.PVRZ~ ~override~
END
ELSE BEGIN
  COPY ~SibelNPC/resources/areas/aria28.mos~ ~override/aria28.mos~
  COPY ~SibelNPC/resources/areas/aria28.tis~ ~override/aria28.tis~

  COPY ~SibelNPC/resources/areas/aria29.mos~ ~override/aria29.mos~
  COPY ~SibelNPC/resources/areas/aria29.tis~ ~override/aria29.tis~

  COPY ~SibelNPC/resources/areas/aria30.mos~ ~override/aria30.mos~
  COPY ~SibelNPC/resources/areas/aria30.tis~ ~override/aria30.tis~

  COPY ~SibelNPC/resources/areas/aria31.mos~ ~override/aria31.mos~
  COPY ~SibelNPC/resources/areas/aria31.tis~ ~override/aria31.tis~

  COPY ~SibelNPC/resources/areas/aria33.mos~ ~override/aria33.mos~
  COPY ~SibelNPC/resources/areas/aria33.tis~ ~override/aria33.tis~

  COPY ~SibelNPC/resources/areas/aria36.mos~ ~override/aria36.mos~
  COPY ~SibelNPC/resources/areas/aria36.tis~ ~override/aria36.tis~
END

/* audio */

COPY ~SibelNPC/resources/audio/FEMALE1#.WAV~ ~override/S!FEMA1#.WAV~
COPY ~SibelNPC/resources/audio/FEMALE10.WAV~ ~override/S!FEMA10.WAV~
COPY ~SibelNPC/resources/audio/FEMALE18.WAV~ ~override/S!FEMA18.WAV~
COPY ~SibelNPC/resources/audio/FEMALE19.WAV~ ~override/S!FEMA19.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1A.WAV~ ~override/S!FEMA1A.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1B.WAV~ ~override/S!FEMA1B.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1C.WAV~ ~override/S!FEMA1C.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1D.WAV~ ~override/S!FEMA1D.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1E.WAV~ ~override/S!FEMA1E.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1F.WAV~ ~override/S!FEMA1F.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1G.WAV~ ~override/S!FEMA1G.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1H.WAV~ ~override/S!FEMA1H.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1I.WAV~ ~override/S!FEMA1I.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1J.WAV~ ~override/S!FEMA1J.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1K.WAV~ ~override/S!FEMA1K.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1L.WAV~ ~override/S!FEMA1L.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1M.WAV~ ~override/S!FEMA1M.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1S.WAV~ ~override/S!FEMA1S.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1T.WAV~ ~override/S!FEMA1T.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1U.WAV~ ~override/S!FEMA1U.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1V.WAV~ ~override/S!FEMA1V.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1W.WAV~ ~override/S!FEMA1W.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1X.WAV~ ~override/S!FEMA1X.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1Y.WAV~ ~override/S!FEMA1Y.WAV~
COPY ~SibelNPC/resources/audio/FEMALE1_.WAV~ ~override/S!FEMA1_.WAV~

COPY ~SibelNPC/resources/audio/AMB_E14A.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2000A.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100A.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100B.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100C.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100D.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100E.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100M.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2100T.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2102.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2103.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2112.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_2113.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_5102A.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_RAT01.wav~ ~override~
COPY ~SibelNPC/resources/audio/AM_RAT04.wav~ ~override~

COPY ~SibelNPC/resources/audio/SS_2100A.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100B.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100C.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100D.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100E.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100F.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100G.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100H.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100I.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100J.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100K.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100M.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100O.wav~ ~override~
COPY ~SibelNPC/resources/audio/SS_2100P.wav~ ~override~

/* EE extras as a separate component */

ACTION_IF (FILE_EXISTS ~engine.lua~) THEN BEGIN
  AT_EXIT ~SibelNPC/Setup-SibelNPCEE.tp2~
END
